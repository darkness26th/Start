//===== rAthena Script ======================================= 
//= Refining NPCs
//===== By: ==================================================
//= Syrus22 (1.1) dafide18 (1.4) Skotlex (1.5)
//===== Current Version: =====================================
//= 3.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Refining NPCs and Metal Salesmen.
//===== Additional Comments: =================================
//= 3.0 Updated several NPC names and locations. [Xantara]
//=     Added WoE map Refiners.
//= 3.1 Added the new refinement & Ore creation NPC's for +11 and above Refinement. [Masao]
//= 3.2 Moved some scripts to Renewal file, other minor changes. [Euphy]
//= 3.2a Added 'disable_items' command. [Euphy]
//= 3.3 Some official script updates. [Euphy]
//= 3.4 Added VIP features. [Euphy]
//============================================================

// Equipment Refiners
//============================================================
prontera,139,183,7	script	ตีบวก#1	10112,{
	callfunc "refinemain","ช่าง",0;
	end;
	
OnInit:
	waitingroom "ตีบวก",0;
	end;
}

function	script	refinemain	{
	disable_items;
	.@npc_name$ = getarg(0);
	.@features = getarg(1);
	mes "["+ .@npc_name$ +"]";
	mes "เลือกอุปกรณ์ ที่ต้องการตีบวกได้เลย";
	next;

	setarray .@indices[1], EQI_ACC_L,EQI_ACC_R,EQI_SHOES,EQI_GARMENT,EQI_HEAD_LOW,EQI_HEAD_MID,EQI_HEAD_TOP,EQI_ARMOR,EQI_HAND_L,EQI_HAND_R,EQI_COSTUME_HEAD_TOP,EQI_COSTUME_HEAD_MID,EQI_COSTUME_HEAD_LOW,EQI_COSTUME_GARMENT,EQI_SHADOW_ARMOR,EQI_SHADOW_WEAPON,EQI_SHADOW_SHIELD,EQI_SHADOW_SHOES,EQI_SHADOW_ACC_R,EQI_SHADOW_ACC_L;
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i) {
		if(getequipisequiped(.@indices[.@i])) {
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "เกิดข้อผิดพลาด #1";
		close;
	}
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) { //custom check
		mes "["+ .@npc_name$ +"]";
		mes "เกิดข้อผิดพลาด #2";
		emotion ET_FRET;
		close;
	}
	//Check to see if the items is already +20
	if(getequiprefinerycnt(.@part) >= 20) {
		mes "["+ .@npc_name$ +"]";
		mes "อุปกรณ์ชิ้นนี้ ตีบวกมาถึงขั้นสูงสุดแล้ว";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@price = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_MATERIAL_ID);
	.@cash_safe_break = 50;

	mes "["+ .@npc_name$ +"]";
	mes "ข้าต้องการ";
	mes "^003366"+getitemname(.@material)+"^000000 และ";
	mes "" + .@price + " Zeny";
	mes "ต้องการดำเนินการต่อหรือไม่?";
	next;
	if(select("ยืนยัน:ยกเลิก") == 2){
		mes "["+ .@npc_name$ +"]";
		mes "ไว้มาใหม่ได้เสมอ";
		close;
	}
	if(getequippercentrefinery(.@part) < 100) {
		mes "["+ .@npc_name$ +"]";
		mes "การตีบวกนี้มีโอกาสสำเร็จ " + getequippercentrefinery(.@part) + "%";
		mes "ต้องการใช้ " + .@cash_safe_break + " Cash เพื่อป้องกันการสูญหาย ของอุปกรณ์หรือไม่?";
		next;
		.@safe_menu = select("ใช้ Cash:ไม่ใช้ Cash:ยกเลิก");
		if(.@safe_menu == 1){
			.@safe_refine = 1;
		}
		else if(.@safe_menu == 3){
			mes "["+ .@npc_name$ +"]";
			mes "โอเค ไว้มาใหม่ได้เสมอ";
			close;
		}
	}
	if((countitem(.@material) < 1) || (Zeny < .@price)) {
		mes "["+ .@npc_name$ +"]";
		mes "คุณมี Zeny ไม่เพียงพอ";
		mes "หรือ " + getitemname(.@material) + " ไม่เพียงพอ...";
		close;
	}
	if(.@safe_refine && #CASHPOINTS < .@cash_safe_break){
		mes "["+ .@npc_name$ +"]";
		mes "คุณมี Cash ไม่เพียงพอ";
		close;
	}
	else if(.@safe_refine){
		#CASHPOINTS -= .@cash_safe_break;
	}
	
	Zeny = Zeny - .@price;
	delitem .@material,1;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
			callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt)) {
		mes "["+ .@npc_name$ +"]";
		emotion ET_FRET;
		mes "คุณกำลังจะโกงการตีบวก!";
		close;
	}

	if(getequippercentrefinery(.@part) <= rand(100)) {
		if(!.@safe_refine){
			failedrefitem .@part;
		}
		mes "["+ .@npc_name$ +"]";
		emotion (!rand(5))?ET_MONEY:ET_HUK;
		if(.@safe_refine)
		mes "การตีบวกไม่สำเร็จ แต่ทุกอย่างยังอยู่เหมือนเดิมนะ";
		else
		mes "การตีบวกไม่สำเร็จ อุปกรณ์ได้สลายหายไปแล้ว...";
		close;
	}
	mes "["+getarg(0)+"]";
	successrefitem .@part;
	emotion ET_SMILE;
	mes "การตีบวกสำเร็จ~";
	close;
}

// Ori/Elu Refiners
//============================================================
prontera,141,178,5	itemshop	แลก-Oridecon	10132,756,984:5
prontera,143,178,3	itemshop	แลก-Elunium	10133,757,985:5

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 by A bunch of people!
//=     Syrus22 - Completely redid the script using functions... also
//=     added the option for auto safe refining and multiple refining.
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Added additional reparimen in Morocc and payon. Added
//=     Christopher the blacksmith in Geffen. Edited some dialogue [kobra_k88]
//= 1.3 New Payon Locations [Darkchild]
//=     Corrected zeny subtraction thx to jpnmania77.[kobra_k88]
//= 1.3a Temporary corrected an exploit. Need to check sources
//=     to fully fix bug [Shinigami]
//=     Fixed repairman prices [shadowlady]
//=     Fixed bug that skips requirements thanks to sir_loon [massdriller]
//=     Fixed itemid error thanks to -Vitamin- [massdriller]
//= 1.4 check again item in refining procedure to avoid
//=     hacker that can change item [dafide18]
//= 1.5 Fixed crashing due to badly used callfunc's [Skotlex]
//=     Lupus, don't rollback this important fix again! >.<
//= 1.5a Corrected an unneeded callfunc, fixed the anti-bot 
//=     exploit ruining the safe refine loop. [Skotlex]
//= 1.5b Fixed Spelling mistakes. [Nexon] 
//= 1.6 Replaced all breaks for ends as per the new script engine [Skotlex]
//= 1.7 Added Einbroch Refiners (Custom names ^^;) and a duplicated BS Shop. [Poki#3]
//= 1.8 Added Lighthalzen Refiners (Custom names again ^^;) [Poki#3]
//= 1.8a Fixed wrong indication thanks to NeoSaro [Lupus]
//= 1.9 Rewrote repairman, removed the Steel from repair cost [DracoRPG]
//= 2.0 Fixed missed equppment presence check. Thx2 Coltaro [Lupus]
//= 2.0a Added weight checks thanks to Neouni [Playtester]
//= 2.0b Fixed the names of Lighthalzen and Einbroch refiners thanks to Maud_Dib [Kargha]
//= 2.1 Removed Duplicates [Silent]
//= 2.2 Changed name from "Emvertacon" to "Emveretarcon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.2b Changed name from "Pharacon" to "Phracon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.3 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.4 Updated Refiner function. cleaner, and less dated. [L0ne_w0lf]
//= 2.5 Rather large update to the refiner and merchants. :D [L0ne_W0lf]
//= 2.6 Fixed a few bugs with creating pure stones. [L0ne_W0lf]
//= 2.7 Refiner function accepts additional paramater. [L0ne_W0lf]
//=     0 = No special features; 1 = new refining features
//=     Updated Repairmen and function. No longer shows menu.
//= 2.7a A couple touch-ups to the repairman function. [L0ne_w0lf]
//= 2.8 Changed the nonexistent variable .@matname$ for getitemname(.@material). (bugreport:2340) [Samuray22]
//= 2.8 Added proper Blacksmith Supplier to Einroch. [L0ne_W0lf]
//=     Updated dated features comment to reflect new usage.
//= 2.8a Small bugfix. (bugreport:2418) [Paradox924X]
//= 2.9 Moved Morocc repairman to Morocc Ruins. [L0ne_W0lf]
//============================================================
